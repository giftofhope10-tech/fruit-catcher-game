buildscript {
    apply from: "variables.gradle"
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    ext.kotlin_version = '1.9.10'
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            project.android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
                if (project.plugins.hasPlugin('kotlin-android') || project.plugins.hasPlugin('kotlin-kapt')) {
                    kotlinOptions {
                        jvmTarget = '17'
                    }
                }
            }
        }
        // Force JVM target 17 on all KotlinCompile tasks, including those from plugins
        project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = "17"
                // Prevent plugins from overriding this
                freeCompilerArgs += ["-Xjvm-default=all"]
            }
        }
    }
}
